// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a=x, b=false, sel=zx, out=resultOfZx);
    Mux16(a=y, b=false, sel=zy, out=resultOfZy);
    Not16(in=resultOfZx, out=notX);
    Not16(in=resultOfZy, out=notY);
    Mux16(a=resultOfZx, b=notX, sel=nx, out=resultOfNx);
    Mux16(a=resultOfZy, b=notY, sel=ny, out=resultOfNy);
    Add16(a=resultOfNx, b=resultOfNy, out=addXY);
    And16(a=resultOfNx, b=resultOfNy, out=andXY);
    Mux16(a=andXY, b=addXY, sel=f, out=resultOfF);
    Not16(in=resultOfF, out=notOutput);
    Mux16(a=resultOfF, b=notOutput, sel=no, out=output);
    Or16(a=false, b=output, out[0..7]=output1, out[8..15]=output2);
    Or16(a=false, b=output, out=out);
    Or8Way(in=output1, out=orOut1);
    Or8Way(in=output2, out=orOut2);
    Or(a=orOut1, b=orOut2, out=notZr);
    Not(in=notZr, out=zr);
    And16(a[15]=true, a[0..14]=false, b=output, out[0..7]=tmp0, out[8..15]=tmp1);
    Or8Way(in=tmp1, out=ng);
}